apply plugin: 'java'
apply plugin: 'maven-publish'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':annotation')
    implementation group: 'com.squareup', name: 'javawriter', version:'2.1.1'
    testImplementation group: 'junit', name: 'junit', version:'4.8.1'
    testImplementation group: 'org.seasar.aptina', name: 'aptina-unit', version:'1.0.0'
}

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = repository_name
            url = repository_url
            credentials {
                username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
            }
        }
    }
    publications {
        release(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            groupId = package_group_id
            artifactId = 'compiler'
            version = package_version
        }
    }
}
